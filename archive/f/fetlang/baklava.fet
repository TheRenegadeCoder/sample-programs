worship Zera (0 * 1)
spank Nora ten times (0 - 10)
lick Enzo eleven times (0 + 11)

(while Nora < Enzo)
while Nora is submissive to Enzo
    (Spencer = number of spaces = abs(Nora))
    if Nora is submissive to Zera
        have Nora spank Spencer (0 - Nora)
    otherwise
        have Nora lick Spencer (0 + Nora)

    (Starr = number of stars = 21 - 2 * Spencer)
    lick Starr twenty one times (0 + 21)
    have Spencer spank Starr (21 - Spencer)
    have Spencer spank Starr (21 - 2 * Spencer)

    (Output " " Spencer times
     Note: Spencer will be zero when this loop exits)
    while Spencer is dominant towards Zera
        make slave moan " "
        spank Spencer

    (Output "*" Starr times
     Note: Starr will be zero when this loop exits)
    while Starr is dominant towards Zera
        make slave moan "*"
        spank Starr

    (Output newline)
    make slave scream

    (Increment Nora)
    lick Nora
