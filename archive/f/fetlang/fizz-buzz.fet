lick Nora (0 + 1)
lick Hunter one hundred and one times (0 + 101)
lick Power10
lick NextPower10 ten times

(Set up single-digit numbers)
worship Number0 (0 * 1)
lick Number1
lick Number2 two times
lick Number3 three times
lick Number4 four times
lick Number5 five times
lick Number6 six times
lick Number7 seven times
lick Number8 eight times
lick Number9 nine times

(while Nora < 101)
while Nora is submissive to Hunter
    (OutputCounter = 0)
    have OutputCounter spank itself

    (if Nora % 3 == 0, output "Fizz" and increment OutputCounter)
    have Remy spank himself (Remy = 0)
    have Nora lick Remy (Remy = Nora)
    tickle Remy three times (Remy = Nora % 3)
    if Remy is Number0
        make slave moan "Fizz"
        lick OutputCounter

    (if Nora % 5 == 0, output "Buzz" and increment OutputCounter)
    have Remy spank himself (Remy = 0)
    have Nora lick Remy (Remy = Nora)
    tickle Remy five times (Remy = Nora % 5)
    if Remy is Number0
        make slave moan "Buzz"
        lick OutputCounter

    (if OutputCounter == 0, output Nora as a number)
    if OutputCounter is Number0
        (Fetlang outputs numbers as words, so need to output each digit as an ASCII digit)

        (CurrentPower10 = Power10)
        have CurrentPower10 spank itself
        have Power10 lick CurrentPower10

        (while CurrentPower10 > 0)
        while CurrentPower10 is dominant towards Number0
            (Didi = Nora)
            have Didi spank herself
            have Nora lick Didi

            (Mona = Nora % CurrentPower10)
            have Mona spank herself
            have Nora lick Mona
            have CurrentPower10 tickle Mona

            (Didi = (Didi - Mona) / CurrentPower10 = floor(Nora / CurrentPower10))
            have Mona spank Didi
            have CurrentPower10 flog Didi

            (Output Didi % 10 as ASCII)
            tickle Didi ten times (Didi % 10)
            if Didi is Number0
                make slave moan "0"

            if Didi is Number1
                make slave moan "1"

            if Didi is Number2
                make slave moan "2"

            if Didi is Number3
                make slave moan "3"

            if Didi is Number4
                make slave moan "4"

            if Didi is Number5
                make slave moan "5"

            if Didi is Number6
                make slave moan "6"

            if Didi is Number7
                make slave moan "7"

            if Didi is Number8
                make slave moan "8"

            if Didi is Number9
                make slave moan "9"

            (CurrentPower10 = floor(CurrentPower10 / 10))
            if CurrentPower10 is not Number1
                flog CurrentPower10 ten times
            otherwise
                have CurrentPower10 spank itself

    (Output newline)
    make slave scream

    (Increment Nora)
    lick Nora

    (If Nora == NextPower10,)
    if Nora is NextPower10
        (Power10 = Power10 * 10)
        worship Power10 ten times

        (NextPower10 = NextPower10 * 10)
        worship NextPower10 ten times
